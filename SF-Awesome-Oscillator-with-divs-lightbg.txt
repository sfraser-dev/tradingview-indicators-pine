// This source code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// © madoqa

//@version=4
study(title="AO", shorttitle = "AO", format=format.price)

ao_fast = input(5,"AO Fast SMA Lenth")
ao_slow = input(34,"AO Slow SMA Length")
src = input(title="LRS Source", defval=hl2)
lbR = input(title="Pivot Lookback Right", defval=5)
lbL = input(title="Pivot Lookback Left", defval=5)
rangeUpper = input(title="Max of Lookback Range", defval=60)
rangeLower = input(title="Min of Lookback Range", defval=5)
plotBull = input(title="Plot Bullish", defval=true)
plotBear = input(title="Plot Bearish", defval=true)

bearColor = color.new(color.red, 20)
bullColor = color.new(color.green, 20)
textColor = color.new(color.white, 20)
noneColor = color.new(color.white, 100)

ao = sma(src,ao_fast)-sma(src,ao_slow)
diff = ao - ao[1]

hline(0)
//plot(ao, color=color.aqua, style=plot.style_line, linewidth=2)
plot(ao, color = diff <= 0 ? #434651 : #b2b5be, style=plot.style_columns, title="")
osc = ao



plFound = na(pivotlow(osc, lbL, lbR)) ? false : true
phFound = na(pivothigh(osc, lbL, lbR)) ? false : true

_inRange(cond) =>
    bars = barssince(cond == true)
    rangeLower <= bars and bars <= rangeUpper

//------------------------------------------------------------------------------
// Regular Bullish

// Osc: Higher Low
oscHL = osc[lbR] > valuewhen(plFound, osc[lbR], 1) and _inRange(plFound[1])

// Price: Lower Low
priceLL = low[lbR] < valuewhen(plFound, low[lbR], 1)

bullCond = plotBull and priceLL and oscHL and plFound

plot(
	 plFound ? osc[lbR] : na,
	 offset=-lbR,
	 title="",
	 linewidth=2,
	 color=(bullCond ? bullColor : noneColor),
	 transp=0
	 )




//------------------------------------------------------------------------------
// Regular Bearish

// Osc: Lower High
oscLH = osc[lbR] < valuewhen(phFound, osc[lbR], 1) and _inRange(phFound[1])

// Price: Higher High
priceHH = high[lbR] > valuewhen(phFound, high[lbR], 1)

bearCond = plotBear and priceHH and oscLH and phFound

plot(
	 phFound ? osc[lbR] : na,
	 offset=-lbR,
	 title="Regular Bearish",
	 linewidth=2,
	 color=(bearCond ? bearColor : noneColor),
	 transp=0
	 )
